plugins {
    id "fabric-loom" version "1.2-SNAPSHOT"
    id "io.github.juuxel.loom-quiltflower" version "1.8.0"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
compileJava.options.encoding = compileTestJava.options.encoding = javadoc.options.encoding = "UTF-8"

group = project.maven_group
archivesBaseName = project.maven_name
version = project.maven_version

repositories {
    mavenCentral()
    maven {
        name = "Jitpack"
        url = "https://jitpack.io"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    annotationProcessor(include(implementation("com.github.llamalad7.mixinextras:mixinextras-fabric:0.2.0-beta.8")))
    include(implementation("net.lenni0451:Reflect:1.1.0"))
}

loom {
    accessWidenerPath = file("src/main/resources/immediatelyfast.accesswidener")
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}
